// Constants
// This is to ensure that a developer does not push to the wrong ready branch namespace.
TARGET_BRANCH_NAME = "master"
MERGE_BRANCH_PREFIX = "ready"
GIT_REPO="git@github.com:jasonparrott/maven-pipeline-example.git"
SSH_AGENT_ID="jasonparrott"

BUILD_COMMAND="mvn clean test"
PACKAGE_COMMAND="mvn package"
VERIFY_COMMAND="mvn test"

// State
inputSHA = "" // Why can't Jenkins tell me this?!
authorEmail = "none"
authorName = "none"
authorFull = "none <none>"
releaseVersion = ""

// Utils
//Does the branch namespace contain "ready", and should therefore be regarded as an integration branch.
def shouldMerge() {
//    return "${BRANCH_NAME}" ==~ /^${MERGE_BRANCH_PREFIX}\/.*/
    return true;
}

def pipelineSuccess() {
    node("master") {
        emailext (
          subject: "Success: ${BRANCH_NAME} (${releaseVersion})",
          body: """
Status: Succeeded
Branch: ${BRANCH_NAME}
Version: ${releaseVersion}
Jenkins URL: ${env.BUILD_URL}
""",
          to: "${authorEmail}"
        )
    }
}

def pipelineFailure(stage, exception) {
    node("master") {
        emailext (
          subject: "Failure: ${BRANCH_NAME} (${releaseVersion}): ${stage}",
          body: """
Status: Failed at stage "${stage}" with "${exception.toString()}"
Branch: ${BRANCH_NAME}
Version: ${releaseVersion}
Jenkins URL: ${env.BUILD_URL}
""",
          to: "${authorEmail}"
        )
    }
}
// Workspace must be cleaned from last run in order to make stash and git clone work.
def cleanNode(name, closure) {
    node(name) {
        deleteDir()
        try {
            closure.call()
        } finally {
            deleteDir()
        }
    }
}
//If we want to send an email when things go wrong, we need to wrap it in a try/catch

def stageWithGuard(name, closure) {
    try {
        stage(name, closure)
    } catch(e) {
        pipelineFailure(name, e)
        throw e
    }
}
//Locking resources in order to for example make sure that only one branch is tried integrated at a time.
def conditionalLock(condition, name, closure) {
    if (condition) {
        println "FIXME: Skipping lock as its not found in the DSL. Should be running under lock: ${name}"
        //lock(name, closure)
        closure.call()
    } else {
        println "Running without lock"
        closure.call()
    }
}



// Stages

println "Parameters: branch: ${BRANCH_NAME}, target: ${TARGET_BRANCH_NAME}, merge: ${shouldMerge()}"

conditionalLock(shouldMerge(), "pipeline-merge-lock") {
    // The following stages are locked to ensure that only one pipeline runs at
    // at a time. The stages in here must contain everything between and
    // including the original git checkout and the final git push of the merged
    // branch.

    stageWithGuard("Checkout") {
        cleanNode("master") {
            buildNumber = currentBuild.number;
            currentBuild.displayName = "${currentBuild.displayName} (${buildNumber})"
            sshagent (credentials: ["${SSH_AGENT_ID}"]) {
                timeout(1) {
                    sh "git clone --no-checkout ${GIT_REPO} ."
                }

                inputSHA = sh(script: "git rev-parse origin/${BRANCH_NAME}", returnStdout: true).trim()
                authorName = sh(script: "git log -1 --format='%an' ${inputSHA}", returnStdout: true).trim()
                authorEmail = sh(script: "git log -1 --format='%ae' ${inputSHA}", returnStdout: true).trim()
                authorFull = "${authorName} <${authorEmail}>"

                timeout(2) {
                    if (shouldMerge()) {
                        sh """
git config user.email "${authorEmail}"
git config user.name "${authorName}"
git checkout "${TARGET_BRANCH_NAME}"
if [ "\$(git branch --contains ${inputSHA} | wc -l)" -gt "0" ]
then
    echo "MERGE ERROR: origin/${BRANCH_NAME} already present in origin/${TARGET_BRANCH_NAME}"
    exit 1
fi
COMMITS="\$(git log --oneline ${TARGET_BRANCH_NAME}..${inputSHA} | wc -l)"
if [ "\${COMMITS}" -gt "1" ] || ! git merge --ff-only ${inputSHA}
then
    git reset --hard origin/${TARGET_BRANCH_NAME}
    git merge --no-ff --no-commit ${inputSHA}
    git commit --author "${authorFull}" --message "Merge branch '${BRANCH_NAME}' into '${TARGET_BRANCH_NAME}'"
fi
git submodule update --init --recursive
"""
                    } else {
                        sh "git checkout ${BRANCH_NAME} && git submodule update --init --recursive"
                    }
                }

                sha = sh(script: "git rev-parse HEAD", returnStdout: true).trim()
                shaish = sh(script: "git rev-parse --short=5 HEAD", returnStdout: true).trim()


            }
            //Stash the whole repository
            stash name: "repo", includes: "**", useDefaultExcludes:false
        }
    }

    stageWithGuard("Build") {
    // only 1 builder, but could do multiple in parallel if needed
        def builders = [
            "standard": {
                cleanNode("standard") {
                    unstash "repo"
                    sh "ls"
                    def mvn_version = 'M350'
                    withEnv( ["PATH+MAVEN=${tool mvn_version}/bin"] ) {
                        sh "${BUILD_COMMAND}"
                    }
                    stash name: "output", includes: "target/**"
                }
            }
        ]

        parallel builders
    }


    stageWithGuard("Push") {
        if (shouldMerge()) {
            cleanNode("master") {
                unstash "repo"

                sshagent (credentials: ["${SSH_AGENT_ID}"]) {
                    timeout(1) {
                        sh """
git push origin ${TARGET_BRANCH_NAME}
git fetch
if [ "\$(git rev-parse origin/${BRANCH_NAME})" = "${inputSHA}" ]
then
	git push origin :${BRANCH_NAME}
fi
"""
                    }
                }
            }
        }
    }
}

// TODO: Any testing/integration/verification done prior to packaging

stageWithGuard("Package") {
    def builders = [
        "standard": {
            cleanNode("linux") {
                unstash "repo"
                def mvn_version = 'M350'
                withEnv( ["PATH+MAVEN=${tool mvn_version}/bin"] ) {
                    sh "${PACKAGE_COMMAND}"
                }
            }
        }
    ]

    parallel builders
}


def verifyLinux(_node, _script) {
    return {
        cleanNode(_node) {
            unstash "repo"
            // TODO: Unstash anything else needed for the verify on this platform
            wrap([$class: "AnsiColorBuildWrapper", "colorMapName": "XTerm"]) {
                def mvn_version = 'M3'
                withEnv( ["PATH+MAVEN=${tool mvn_version}/bin"] ) {
                    sh _script
                }
            }
        }
    }
}

stageWithGuard("Verify") {
    def builders = [
        "linux":   verifyLinux("linux", "${VERIFY_COMMAND}")
    ]

    parallel builders
}



stageWithGuard("Upload") {
    cleanNode("master") {
        unstash "repo"
        unstash "output"
        println "Artifact uploaded to REPOSITORY... FIX ME TO DO SOMETHING USEFUL HERE"
    }
}

stageWithGuart("Deploy") {
    cleanNode("master") {
        println "Deploying to target....... FIX ME TO DO SOMETHING USEFUL HERE"
    }
}


stageWithGuard("Dance") {
    pipelineSuccess()
    echo '''
└─(･◡･)─┐
└─(･◡･)─┘
┌─(･◡･)─┘
┌─(･◡･)─┐
└─(･◡･)─┐
└─(･◡･)─┘
'''
}